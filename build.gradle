buildscript {
   repositories {
      mavenCentral()
      maven { url "https://oss.sonatype.org/content/groups/public"}
   }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.4'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'pl.allegro.tech.build.axion-release'
sourceCompatibility = 1.8
defaultTasks 'clean', 'build'
ext.rundeckPluginVersion = '1.1'
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'v'
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation{
        extendsFrom pluginLibs
    }
}
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation "org.slf4j:slf4j-api:1.7.36"
    implementation (group: 'org.rundeck', name: 'rundeck-core', version: '3.4.10-20220118') {
        exclude group: "com.google.guava"
    }
    implementation "com.amazonaws:aws-java-sdk-core:${awsSdkVersion}"
    implementation "com.amazonaws:aws-java-sdk-sts:${awsSdkVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.7.1"
    implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

    pluginLibs group: 'stax', name: 'stax', version: '1.2.0'
    pluginLibs group: 'javax.xml.stream', name: 'stax-api', version: '1.0'

    pluginLibs (group: 'com.amazonaws', name: 'aws-java-sdk-ec2', version: "${awsSdkVersion}") {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }

    pluginLibs (group: 'com.amazonaws', name: 'aws-java-sdk-sts', version: "${awsSdkVersion}") {
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }

    testImplementation "org.codehaus.groovy:groovy-all:2.3.7"
    testImplementation "org.spockframework:spock-core:0.7-groovy-2.0"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation 'org.objenesis:objenesis:3.1'
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name': 'EC2 Nodes Plugin'
        attributes 'Rundeck-Plugin-Description': 'Resource Model Source plugin that provides Amazon EC2 Instances as nodes.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.2.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,aws,ec2,resource model'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/rundeck-ec2-nodes-plugin'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true', 'Rundeck-Plugin-Libs-Load-First':'false'
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.dtolabs.rundeck.plugin.resources.ec2.EC2ResourceModelSourceFactory', 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Rundeck-Plugin-File-Version': version
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
