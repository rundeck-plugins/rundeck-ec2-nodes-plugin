buildscript {
   repositories {
      mavenCentral()
   }
}

plugins {
    id 'java'
    id 'groovy'
    id 'idea'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

group = 'com.rundeck.plugins'
ext.publishName = "EC2 Nodes Plugin ${project.version}"
ext.publishDescription = project.description ?: 'Produces Rundeck Nodes from AWS EC2'
ext.githubSlug = 'rundeck-plugins/rundeck-ec2-nodes-plugin'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

java {
    sourceCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}
defaultTasks 'clean', 'build'
ext.rundeckPluginVersion = '1.1'
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = ''           // NO "v" prefix - see PLUGIN_TAGGING_ARCHITECTURE.md
        versionSeparator = ''
    }
    versionCreator 'simple'  // Use simple version creator (just tag name)
}
project.version = scmVersion.version

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs {
    }

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}
repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    implementation libs.slf4jApi
    implementation(libs.rundeckCore) {
        exclude group: "com.google.guava"
    }
    implementation libs.bundles.awsSdk
    implementation libs.jacksonDatabind
    implementation libs.commonsBeanutils
    
    // Add secure commons-lang3 to provide alternative to vulnerable commons-lang 2.6
    implementation(libs.commonsLang3)

    pluginLibs(libs.awsSdkEc2) {
        exclude group: "org.apache.httpcomponents", module: "httpclient"
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }
    pluginLibs(libs.awsSdkSts) {
        exclude group: "org.apache.httpcomponents", module: "httpclient"
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
    }

    testImplementation libs.bundles.testLibs
}

configurations.all {
    resolutionStrategy {
        // Replace vulnerable commons-lang with secure commons-lang3
        dependencySubstitution {
            substitute module('commons-lang:commons-lang') using module("org.apache.commons:commons-lang3:${libs.versions.commonsLang3.get()}")
        }
    }
}

// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}


jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name': 'EC2 Nodes Plugin'
        attributes 'Rundeck-Plugin-Description': 'Resource Model Source plugin that provides Amazon EC2 Instances as nodes.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.2.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,aws,ec2,resource model'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/rundeck-ec2-nodes-plugin'
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 'Rundeck-Plugin-Archive': 'true', 'Rundeck-Plugin-Libs-Load-First':'false'
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.dtolabs.rundeck.plugin.resources.ec2.EC2ResourceModelSourceFactory', 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Rundeck-Plugin-File-Version': version
    }
}

test {
    useJUnitPlatform()
}
//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

nexusPublishing {
    packageGroup = 'com.rundeck.plugins'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}

apply from: "${rootDir}/gradle/publishing.gradle"

// Add PackageCloud repository
publishing {
    repositories {
        maven {
            name = "PackageCloudTest"
            url = uri("https://packagecloud.io/pagerduty/rundeckpro-test/maven2")
            authentication {
                header(HttpHeaderAuthentication)
            }
            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "Bearer " + (System.getenv("PKGCLD_WRITE_TOKEN") ?: project.findProperty("pkgcldWriteToken"))
            }
        }
    }
}